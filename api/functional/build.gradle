apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.slf4j:slf4j-log4j12:2.0.17")
    implementation("org.apache.commons:commons-text:1.13.1")

    testImplementation(platform('org.junit:junit-bom:5.13.+'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.13.+')
    testImplementation("org.junit.platform:junit-platform-launcher:1.13.+")
    testImplementation("org.awaitility:awaitility:4.3.+")
    testImplementation("org.assertj:assertj-core:3.27.+")
    testImplementation("com.github.tomakehurst:wiremock:3.0.+")
//    testImplementation('io.cucumber:cucumber-java:7.23.+')
//    testImplementation('io.cucumber:cucumber-junit:7.23.+')
    testImplementation('io.rest-assured:rest-assured:5.5.+')
    testImplementation('org.skyscreamer:jsonassert:1.5.+')
    testImplementation("net.datafaker:datafaker:2.4.3")
}

// Test will be called from make target stubbed-functional
// You can also run it manually if you have the app started
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standard_out", "standard_error"
        showStandardStreams = true
    }
    systemProperty 'serviceEndpoint', System.getenv("SERVICE_ENDPOINT") ? System.getenv("SERVICE_ENDPOINT")  : "http://localhost:8080"

    // Print test summary details
    afterSuite { desc, result ->
        if (!desc.parent) {
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
            def startItem = '|  ', endItem = '  |'
            def repeatLength = startItem.length() + output.length() + endItem.length()
            println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
    }
}
